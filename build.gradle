buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'pmd'

repositories {
    jcenter()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-core:2.7.3'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'joda-time:joda-time:2.9.7'
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:recyclerview-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'info.guardianproject.netcipher:netcipher:1.2.1'
	compile 'com.android.support.constraint:constraint-layout:+'

    testCompile 'junit:junit:4.12'
}

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.1'

    defaultConfig {
        vectorDrawables.generatedDensities = ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    }
    // Flag to tell aapt to keep the attribute ids around
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        compileOptions.encoding = "UTF-8"

        // Use Java 1.7, requires minSdk 8
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    
    lintOptions {
        checkReleaseBuilds false
        abortOnError false

        // Translations are crowd-sourced
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    sourceSets {
        main {
            assets.srcDirs = ['assets']
        }
    }
}

pmd {
    toolVersion = '5.5.1'
    consoleOutput = true
}

task pmd(type: Pmd) {
    dependsOn 'assembleDebug'
    ruleSetFiles = files("${project.rootDir}/config/pmd/rules.xml")
    ruleSets = [] // otherwise defaults clash with the list in rules.xml
    source 'src/main/java/org/quantumbadger'
    include '**/*.java'
}
