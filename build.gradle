buildscript {
    repositories {
        jcenter()
		google()
	}
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'pmd'

repositories {
    jcenter()
	google()
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.3'
    implementation 'org.apache.commons:commons-lang3:3.7'
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'info.guardianproject.netcipher:netcipher:1.2.1'

    testImplementation 'junit:junit:4.12'
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        vectorDrawables.generatedDensities = ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    }
    // Flag to tell aapt to keep the attribute ids around
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        compileOptions.encoding = "UTF-8"

        // Use Java 1.7, requires minSdk 8
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    
    lintOptions {
        checkReleaseBuilds false
        abortOnError false

        // Translations are crowd-sourced
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    sourceSets {
        main {
            assets.srcDirs = ['assets']
        }
    }
}

pmd {
    toolVersion = '5.6.0'
    consoleOutput = true
}

task pmd(type: Pmd) {
    dependsOn 'assembleDebug'
    ruleSetFiles = files("${project.rootDir}/config/pmd/rules.xml")
    ruleSets = [] // otherwise defaults clash with the list in rules.xml
    source 'src/main/java/org/quantumbadger'
    include '**/*.java'
}
