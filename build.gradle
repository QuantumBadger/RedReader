buildscript {
	repositories {
		jcenter()
		google()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:4.0.1'
	}
}

repositories {
	jcenter()
	google()
}

apply plugin: 'com.android.application'
apply plugin: 'pmd'
apply plugin: 'checkstyle'

dependencies {
	implementation 'androidx.appcompat:appcompat:1.2.0'
	implementation 'androidx.recyclerview:recyclerview:1.1.0'
	implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
	implementation 'com.google.android.material:material:1.2.0'

	implementation 'com.fasterxml.jackson.core:jackson-core:2.11.2'
	implementation 'org.apache.commons:commons-lang3:3.11'
	implementation 'org.apache.commons:commons-text:1.9'
	implementation 'joda-time:joda-time:2.10.6'
	implementation 'com.squareup.okhttp3:okhttp:3.12.12'
	implementation 'info.guardianproject.netcipher:netcipher:1.2.1'
	implementation 'com.google.android.exoplayer:exoplayer-core:2.9.6'
	implementation 'com.google.android.exoplayer:exoplayer-ui:2.9.6'

	testImplementation 'junit:junit:4.13'
}

android {
	compileSdkVersion 29
	buildToolsVersion "29.0.3"

	defaultConfig {
		applicationId "org.quantumbadger.redreader"
		minSdkVersion 14
		targetSdkVersion 29
		versionCode 92
		versionName "1.12"

		vectorDrawables.generatedDensities = ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
	}
	// Flag to tell aapt to keep the attribute ids around
	aaptOptions {
		additionalParameters "--no-version-vectors"
	}

	buildTypes {
		release {
			minifyEnabled false
		}
	}

	compileOptions {
		encoding = "UTF-8"
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	lintOptions {
		checkReleaseBuilds false
		abortOnError true

		// Translations are crowd-sourced
		disable 'MissingTranslation'
		disable 'MissingQuantity'

		// Temporarily disabled
		disable 'ExtraTranslation'
		disable 'ImpliedQuantity'
	}

	packagingOptions {
		exclude 'META-INF/*'
	}

	sourceSets {
		main {
			assets.srcDirs = ['assets']
		}
	}
}

pmd {
	toolVersion = '6.24.0'
	consoleOutput = true
}

task pmd(type: Pmd) {
	dependsOn 'assembleDebug'
	ruleSetFiles = files("${project.rootDir}/config/pmd/rules.xml")
	ruleSets = [] // otherwise defaults clash with the list in rules.xml
	source 'src/main/java/org/quantumbadger'
	include '**/*.java'
}

checkstyle {
	configFile rootProject.file("${project.rootDir}/config/checkstyle/checkstyle.xml")
	ignoreFailures false
	showViolations true
	toolVersion = "8.35"
}

task Checkstyle(type: Checkstyle) {
	source 'src/main/java/org/quantumbadger'
	ignoreFailures false
	showViolations true
	include '**/*.java'
	classpath = files()
}
