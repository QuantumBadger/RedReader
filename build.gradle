buildscript {
	repositories {
		mavenCentral()
		google()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:7.0.4'
	}
}

repositories {
	mavenCentral()
	google()
}

apply plugin: 'com.android.application'
apply plugin: 'pmd'
apply plugin: 'checkstyle'

dependencies {
	implementation 'androidx.annotation:annotation:1.3.0'
	implementation 'androidx.appcompat:appcompat:1.4.0'
	implementation 'androidx.recyclerview:recyclerview:1.2.1'
	implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
	implementation 'androidx.preference:preference:1.1.1'
	implementation 'com.google.android.material:material:1.4.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
	implementation 'androidx.fragment:fragment:1.4.0'

	implementation 'com.fasterxml.jackson.core:jackson-core:2.13.1'
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'org.apache.commons:commons-text:1.9'
	implementation 'joda-time:joda-time:2.10.13'
	implementation 'com.squareup.okhttp3:okhttp:3.12.13'
	implementation 'info.guardianproject.netcipher:netcipher:1.2.1'
	implementation 'com.google.android.exoplayer:exoplayer-core:2.16.1'
	implementation 'com.google.android.exoplayer:exoplayer-ui:2.16.1'
	implementation 'com.github.luben:zstd-jni:1.5.1-1@aar'

	testImplementation 'junit:junit:4.13.2'

	androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
	androidTestImplementation 'androidx.test:rules:1.4.0'
	androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
}

android {
	compileSdkVersion 31
	buildToolsVersion "32.0.0"
	ndkVersion "23.1.7779620"

	defaultConfig {
		applicationId "org.quantumbadger.redreader"
		minSdkVersion 16
		targetSdkVersion 30
		versionCode 103
		versionName "1.19"

		vectorDrawables.generatedDensities = ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
		testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
	}
	// Flag to tell aapt to keep the attribute ids around
	aaptOptions {
		additionalParameters "--no-version-vectors"
	}

	buildTypes.all {
		minifyEnabled true
		shrinkResources false

		proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
	}

	compileOptions {
		encoding = "UTF-8"
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	lintOptions {
		checkReleaseBuilds false
		abortOnError true
		warningsAsErrors true

		error 'DefaultLocale'

		// Translations are crowd-sourced
		disable 'MissingTranslation'
		disable 'MissingQuantity'
		disable 'ExtraTranslation'

		// Temporarily disabled
		disable 'ImpliedQuantity'
		disable 'InflateParams'
		disable 'ObsoleteSdkInt'
		disable 'OldTargetApi'
		disable 'AndroidGradlePluginVersion'

		// Too many false positives
		disable 'UnusedResources'

		// Not an issue
		disable 'ViewConstructor'
		disable 'GradleDependency'

		// Caused by 3rd party dependencies/jetification
		disable 'ObsoleteLintCustomCheck'
	}

	packagingOptions {
		exclude 'META-INF/*'
	}

	sourceSets {
		main {
			assets.srcDirs = ['assets']
		}
	}

	testOptions {
		animationsDisabled = true
	}
}

pmd {
	toolVersion = '6.36.0'
	consoleOutput = true
}

task pmd(type: Pmd) {
	dependsOn 'assembleDebug'
	ruleSetFiles = files("${project.rootDir}/config/pmd/rules.xml")
	ruleSets = [] // otherwise defaults clash with the list in rules.xml
	source 'src/main/java/org/quantumbadger'
	include '**/*.java'
}

checkstyle {
	configFile rootProject.file("${project.rootDir}/config/checkstyle/checkstyle.xml")
	ignoreFailures false
	showViolations true
	maxWarnings 0
	toolVersion = "8.35"
}

task Checkstyle(type: Checkstyle) {
	source 'src/main/java/org/quantumbadger'
	ignoreFailures false
	showViolations true
	include '**/*.java'
	classpath = files()
}
